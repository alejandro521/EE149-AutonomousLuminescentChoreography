target C {
  platform: {
      name: "rp2040",
      board: "pololu_3pi_2040_robot"
  },
  threading: false,
}

import MotorsWithFeedback from "../lib/MotorsWithFeedback.lf"
import Encoders from "../lib/Encoders.lf"
import GyroAngle from "../lib/IMU.lf"

preamble {=
  #include <stdlib.h>
  #include <stdio.h>
  #include <pico/stdlib.h>
  #include <hardware/gpio.h>
  #include <math.h>

  #define GYRO_CALIBRATION_TIMESTEPS 1600
=}

reactor Pololu {
  input drive_mode:int; //0 = stopped, 1 = driving, 2 = turning
  input drive_direction:bool; //0 = forwards or left, 1 = backwards or right
  input drive_amount:float; // in angles for turning, in metric units for driving
  input speed:float;
  output facing_angle:float;
  output current_mode:string;
  output completion_notify:bool;

  gyro = new GyroAngle();
  encoders = new Encoders();
  motors = new MotorsWithFeedback();
  encoders.left -> motors.left;
  encoders.right -> motors.right;

  state left_speed: float = 0.15
  state right_speed: float = 0.15

  state step_counter: int = 0
  state gyro_actual_z: float = 0.0
  state gyro_start: float = 0.0
  state gyro_bias: float = 0.0

  timer t(0, 10 ms);
  
  reaction(startup) -> facing_angle {=
    lf_set(facing_angle, 0);
  =}

  reaction(t) -> gyro.trigger, encoders.trigger, facing_angle {=
    lf_set(gyro.trigger, true);
    lf_set(encoders.trigger, true);
  =}


  initial mode CALIBRATING_GYRO {
    reaction(startup) gyro.z -> motors.left_speed, motors.right_speed, current_mode {=
      lf_set(motors.left_speed, 0);
      lf_set(motors.right_speed, 0);
      self->step_counter = 0;
      self->gyro_start = gyro.z->value;
      lf_set(current_mode, "Calibrating");
    =}

    reaction(t) gyro.z -> STOPPED, motors.left_speed, motors.right_speed, current_mode {=
      self->step_counter += 1;
      if (self->step_counter >= GYRO_CALIBRATION_TIMESTEPS) {
        self->gyro_bias = (gyro.z->value - self->gyro_start) / self->step_counter; //gyro.z accumulates bias over time, so we divide the total change by the timesteps passed
        lf_set(motors.left_speed, 0);
        lf_set(motors.right_speed, 0);
        lf_set(current_mode, "Stopped");
        lf_set_mode(STOPPED);
      }
    =}
  }

  mode STOPPED {
    reaction(drive_mode) drive_direction, gyro.z -> TURNING, DRIVING, motors.left_speed, motors.right_speed, current_mode {=
      if (drive_mode->value == 2) {
        if (drive_direction->value) {
          lf_set(motors.left_speed, self->left_speed);
          lf_set(motors.right_speed, -self->right_speed);
        } else {
          lf_set(motors.left_speed, -self->left_speed);
          lf_set(motors.right_speed, self->right_speed);
        }
        self->step_counter = 0;
        self->gyro_start = gyro.z->value;
        lf_set(current_mode, "Turning");
        lf_set_mode(TURNING);
      }

      else if (drive_mode->value == 1) {
        if (drive_direction->value) {
          lf_set(motors.left_speed, -self->left_speed);
          lf_set(motors.right_speed, -self->right_speed);
        } else {
          lf_set(motors.left_speed, self->left_speed);
          lf_set(motors.right_speed, self->right_speed);
        }
        self->step_counter = 0;
        lf_set(current_mode, "Driving");
        lf_set_mode(DRIVING);
      }
    =}

    reaction(speed) -> completion_notify {=
      self->left_speed = speed->value;
      self->right_speed = speed->value;
      lf_set(completion_notify, true);
    =}
  }

  mode TURNING {
    reaction(t) gyro.z, drive_amount -> STOPPED, motors.left_speed, motors.right_speed, facing_angle, current_mode, completion_notify {=
      self->step_counter += 1;
      self->gyro_actual_z = (91.5f / 90.0f)*(gyro.z->value - self->gyro_start) - (self->gyro_bias * self->step_counter);
      if (abs(self->gyro_actual_z) > drive_amount->value) {
        lf_set(motors.left_speed, 0);
        lf_set(motors.right_speed, 0);
        lf_set(current_mode, "Stopped");
        lf_set(completion_notify, true);
        lf_set_mode(STOPPED);
      }
      lf_set(facing_angle, fmodf(self->gyro_actual_z, 360.0f));
    =}
  }

  mode DRIVING {
    reaction(t) drive_amount -> STOPPED, motors.left_speed, motors.right_speed, facing_angle, current_mode, completion_notify {=
      self->step_counter += 1;
      if (self->step_counter > drive_amount->value) {
        lf_set(motors.left_speed, 0);
        lf_set(motors.right_speed, 0);
        lf_set(current_mode, "Stopped");
        lf_set(completion_notify, true);
        lf_set_mode(STOPPED);
      }
    =}
  }

}